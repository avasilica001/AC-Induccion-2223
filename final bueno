LED_R		EQU	P2.0
BUZZER		EQU	P2.1
MAS_BTN		EQU	P3.0
MENOS_BTN	EQU	P3.1
ONOFF_BTN	EQU	P3.2
SENSOR		EQU	P3.3
;ADC_TEMP	EQU	P5.0
DISPLAY		EQU	0X29
FUEGO		EQU	0X30
DP_A		EQU	P0.0
DP_B		EQU	P0.1
DP_C		EQU	P0.2
DP_D		EQU	P0.3
DP_E		EQU	P0.4
DP_F		EQU	P0.5
DP_G		EQU	P0.6
ESTADO		EQU	0X20
EVENTO		EQU	0X21
CONT1		EQU	0X27
CONT2		EQU	0X28
TICK100MS	EQU	0X22.0
MS100		EQU	0X23
MS1002		EQU	0X24
TIEMPO		EQU	0X25
IEN0		EQU	0XA8
DP_ENCENDIDO	EQU	0X22.1
APAGANDO	EQU	0X22.2
VECESREPETIR	EQU	0X26
PWMP		EQU	0X31
PWM0		EQU	0X32
TICKADC		EQU	0X22.3
TICKTEMP	EQU	0X22.4
ADCON		EQU	0XC5
ADCH		EQU	0XC6
TEMPERATURA	EQU	0X33
CALENTANDO	EQU	0X22.5

ORG 0X00
	AJMP 	INICIO


ORG 0x0B
INTERRUPCION_TIMER:
	ACALL 	INT_TIMER
	RETI

ORG 0x53
INTERRUPCION_ADC:
	ACALL 	INT_ADC
	RETI

ORG 0X80
INICIO:
	ACALL 	INICIALIZACIONES

BUCLE:
	ACALL 	MAQUINAESTADOS
	AJMP	BUCLE

INICIALIZACIONES:
; 	MOV	A,#12	Probar que debugea bien
	MOV	ESTADO,#0
	MOV	EVENTO,#0
	MOV 	CONT1,#0
	MOV 	CONT2,#0
	MOV 	MS100, #0
	MOV	MS1002, #0 
	MOV	TIEMPO, #0 
	MOV	VECESREPETIR, #1
	MOV	FUEGO,#00 
	MOV	DISPLAY,#00 
	MOV	PWMP, #00
	MOV	PWM0, #00
	MOV	TEMPERATURA, #00
	CLR	TICKADC
	CLR	TICKTEMP
	CLR	A
	CLR	C
	CLR	DP_ENCENDIDO
	CLR	APAGANDO
	CLR	LED_R
	CLR	BUZZER
	CLR	MAS_BTN
	CLR	MENOS_BTN
	CLR	ONOFF_BTN
	CLR	SENSOR
	CLR 	TICK100MS
	CLR	CALENTANDO
	CLR	DP_A
	CLR	DP_B
	CLR	DP_C
	CLR	DP_D 
	CLR	DP_E
	CLR	DP_F
	CLR	DP_G
	CLR	LED_R
	ORL	TMOD,#00000010b
	ANL	TMOD,#11110010b; INICIALIZAMOS EL TIMER DE 8 BITS
	ACALL	ENCENDER_TIMER
;	CLR	ADC_TEMP
	RET


MAQUINAESTADOS:
	MOV	A,ESTADO
	RL	A
	MOV 	DPTR,#EST_TABL
	JMP 	@A+DPTR

EST_TABL:
	AJMP	ESTADO0
	AJMP	ESTADO1
	AJMP	ESTADO2
	AJMP	ESTADO3
	AJMP	ESTADO4
	AJMP	ESTADO5
	;AJMP	ESTADO6


;*********************************************** ESTADO 0 --> REPOSO
ESTADO0:
	ACALL	GEN_EVE0
	MOV	A,EVENTO
	RL 	A; ROTA ACUM A LA IZQ
	MOV 	DPTR,#EV0_TBL
	JMP 	@A+DPTR

EV0_TBL:
	AJMP 	EV00_ESPERA ;EVENTO VACIO
	AJMP 	EV01_PULSADOON ;EVENTO AL PULSAR EL BOTON ON

	

GEN_EVE0:
	;SI SE HA PULSADO EL BOTON DE ON
	JB 	ONOFF_BTN, CON00
	RET


EV00_ESPERA:
	RET

EV01_PULSADOON:
	MOV	FUEGO, #00
	CLR	APAGANDO
	SETB	DP_ENCENDIDO
	ACALL	ACTUALIZAR_DP
	ACALL	ACTUALIZAR_PWM
	SETB	LED_R
	MOV 	ESTADO,#01
	RET

CON00:
	MOV 	EVENTO,#01
	RET

;*********************************************** ESTADO 1 --> PITIDO
ESTADO1:
	ACALL	GEN_EVE1
	MOV	A,EVENTO
	RL 	A; ROTA ACUM A LA IZQ
	MOV 	DPTR,#EV1_TBL
	JMP 	@A+DPTR

EV1_TBL:
	AJMP 	EV10_ESPERA ;EVENTO VACIO
	AJMP 	EV11_TERMINARSIGUIENTE;EVENTO AL PULSAR EL BOTON ON
	AJMP	EV12_SEGUIR
	AJMP	EV13_TERMINARREPOSO

GEN_EVE1:
	;PITIDO
	SETB 	BUZZER
	MOV 	A, #0X2
	CLR	C
	SUBB	A, MS100
	;SE HA LLEGADO A LOS 200MS
	JZ	CON11
	JB 	TICK100MS,CON12
	MOV 	EVENTO,#0
	RET

EV10_ESPERA:
	RET

CON11:
	JB	APAGANDO, EV13_TERMINARREPOSO
	JNB	APAGANDO, EV11_TERMINARSIGUIENTE
	RET

CON12:
	MOV 	EVENTO, #02
	RET

EV11_TERMINARSIGUIENTE:
	CLR 	BUZZER
	MOV 	MS100, #0
	CLR 	TICK100MS
	MOV 	ESTADO,#02
	MOV	EVENTO,#00
        RET

EV12_SEGUIR:
 	CLR 	TICK100MS
	INC 	MS100
	RET

EV13_TERMINARREPOSO:
	;ACALL	APAGAR_TIMER
	ACALL	ENCENDER_ADC
	ACALL	APAGAR_DP
	CLR	BUZZER
	MOV 	MS100,#0
	CLR	TICK100MS
	MOV	ESTADO, #05
	MOV	EVENTO, #00
	RET
;*********************************************** ESTADO 2 --> PARPADEO
ESTADO2:
        ACALL	GEN_EVE2
        MOV 	A,EVENTO
        RL 	A
        MOV 	DPTR,#EV2_TABL
        JMP 	@A+DPTR


EV2_TABL:
	AJMP 	EV20_ESPERA
        AJMP 	EV21_ENCENDER
	AJMP	EV22_BUCLE
	AJMP	EV23_DETECTADO
	AJMP	EV24_TERMINADO
	AJMP	EV25_APAGARDP
	AJMP	EV26_DETECTADOSEGUIR

GEN_EVE2:
	JNB 	ONOFF_BTN, COND24
	JB 	SENSOR, COND20
	;CONTADOR DE 500MS / MEDIO SEGUNDO
	MOV 	A, #0X5
	CLR	C
	SUBB	A, MS100
	;SE HA LLEGADO A LOS 500MS
	JZ	COND21
	;CONTADOR DE 15 SEGUNDOS
	;SE REPETIRA 1 VEZ
	MOV	A, #150
	CLR	C
	SUBB	A, MS1002
	;SE HA LLEGADO A LOS 15 SEG
	JZ	COND23
	JB 	TICK100MS,COND22
	MOV 	EVENTO,#0
	RET

COND20:
	MOV	A,FUEGO
	JZ	EV23_DETECTADO
	JNZ	EV26_DETECTADOSEGUIR
	RET

COND21:
	JB	DP_ENCENDIDO, EV25_APAGARDP
	JNB	DP_ENCENDIDO, EV21_ENCENDER
	RET

COND22:
	MOV 	EVENTO, #0X02
	RET

COND23:
	MOV	MS100,#0 
	MOV	MS1002,#0
	DEC	VECESREPETIR
	MOV	A, VECESREPETIR
	MOV	EVENTO,#00 
	JZ	EV24_TERMINADO
	RET

COND24:
	MOV	EVENTO, #04
	RET

EV20_ESPERA:
	RET

EV21_ENCENDER:
	MOV	MS100,#0
	CLR 	TICK100MS
	ACALL	ACTUALIZAR_DP
	SETB	DP_ENCENDIDO
	RET

EV22_BUCLE:
	CLR	TICK100MS
	INC	MS100
	INC	MS1002
	RET

EV23_DETECTADO:
	MOV	VECESREPETIR, #04
	MOV	ESTADO,#03
	MOV	EVENTO,#00 
	ACALL	ACTUALIZAR_DP
	MOV 	MS100, #0
	MOV 	MS1002, #0
	CLR 	TICK100MS
	ACALL	ACTUALIZAR_PWM
        RET

EV24_TERMINADO:
	SETB	APAGANDO
	CLR	LED_R
	ACALL 	APAGAR_PWM
	ACALL	APAGAR_DP
	MOV 	MS100, #0
	MOV 	MS1002, #0
	MOV	VECESREPETIR,#1 
	CLR	TICK100MS
	MOV	ESTADO,#01 ;PITIDO
        RET

EV25_APAGARDP:
	MOV	MS100, #0
	CLR 	TICK100MS
	CLR	DP_ENCENDIDO
	ACALL	APAGAR_DP
	RET

EV26_DETECTADOSEGUIR:
	ACALL 	ACTUALIZAR_PWM
	MOV	VECESREPETIR, #02
	MOV	ESTADO,#04
	MOV	EVENTO,#00
	ACALL	ACTUALIZAR_DP
	MOV 	MS100, #0
	MOV 	MS1002, #0
	CLR 	TICK100MS
	RET


;*********************************************** ESTADO 3 --> RECIPIENTE CORRECTO PUESTO
ESTADO3:
        ACALL	GEN_EVE3
        MOV 	A,EVENTO
        RL 	A
        MOV 	DPTR,#EV3_TABL
        JMP 	@A+DPTR


EV3_TABL:
	AJMP 	EV30_ESPERA
        AJMP 	EV31_1MIN
	AJMP	EV32_BUCLE
	AJMP	EV33_FUEGO

GEN_EVE3:
	JNB 	ONOFF_BTN, COND34
	JB	MAS_BTN, COND33
	;CONTADOR DE 15 SEGUNDOS
	;SE REPETIRA 4 VECES
	MOV	A, #150
	CLR	C
	SUBB	A, MS100
	;SE HA LLEGADO A LOS 15 SEG
	JZ	COND31
	JB 	TICK100MS,COND32
	MOV 	EVENTO,#0
	RET


COND31:
	MOV	MS100,#0 
	DEC	VECESREPETIR
	MOV	A, VECESREPETIR
	MOV	EVENTO,#00 
	JZ	EV31_1MIN
	RET

COND32:
	MOV	EVENTO, #0X02
	RET

COND33:
	MOV 	A, FUEGO
	JZ 	EV33_FUEGO
	RET

COND34:
	MOV	EVENTO, #01
	RET

EV30_ESPERA:
	RET

EV31_1MIN:
	SETB	APAGANDO
	CLR	LED_R
	ACALL	APAGAR_DP
	MOV 	MS100, #0
	CLR	TICK100MS
	MOV	ESTADO,#01
        RET

EV32_BUCLE:
	CLR	TICK100MS
	INC	MS100
	RET

EV33_FUEGO:
	SETB	CALENTANDO
	MOV	VECESREPETIR,#1 
	MOV	FUEGO,#01 
	ACALL	ACTUALIZAR_DP
	MOV 	MS100, #0
	CLR 	TICK100MS
	MOV	ESTADO,#04
	MOV	EVENTO, #00
        RET
;*********************************************** ESTADO 4 --> FOGON FUNCIONANDO
ESTADO4:
        ACALL	GEN_EVE4
        MOV 	A,EVENTO
        RL 	A
        MOV 	DPTR,#EV4_TABL
        JMP 	@A+DPTR


EV4_TABL:
	AJMP	EV40_ESPERA
	AJMP	EV41_RESTAR
	AJMP	EV42_SUMAR
	AJMP	EV43_REPOSOH
	AJMP	EV44_SINRECIPIENTE

GEN_EVE4:
	JNB	ONOFF_BTN, COND43
	JB	SENSOR, COND40
	JNB	SENSOR, COND44
	RET

EV40_ESPERA:
	RET

COND40:
	JB	MAS_BTN, CON42
	JB	MENOS_BTN, CON41
	RET

COND44:
	MOV 	EVENTO,#04
	RET 
CON41:
	MOV 	A,FUEGO
	;SI ESTAMOS EN 0 NO SE PUEDE RESTAR MAS
	JNZ 	EV41_RESTAR
	RET

CON42:
	MOV 	A,FUEGO
	;SI ESTAMOS EN P (10) NO SE PUEDE SUMAR MAS
	CJNE	A,#10D,EV42_SUMAR	;SOLO SUMA SI EL DP NO MUESTRA P
	RET

COND43:
	MOV 	EVENTO, #03
	RET

EV42_SUMAR:
	ACALL	SUMAR
	RET

EV41_RESTAR:
	JNZ	RESTAR
	RET

EV43_REPOSOH:
	SETB	APAGANDO
	CLR	LED_R
	ACALL 	APAGAR_PWM
	ACALL	APAGAR_DP
	MOV	VECESREPETIR, #01
	MOV	ESTADO,#01 ;PITIDO
	RET

EV44_SINRECIPIENTE:
	ACALL 	APAGAR_PWM
	MOV	VECESREPETIR, #02
	MOV	ESTADO, #02
	RET

SUMAR:
	INC	FUEGO
	ACALL	ACTUALIZAR_DP
	ACALL	ACTUALIZAR_PWM
	RET

RESTAR:
	DEC	FUEGO
	ACALL	ACTUALIZAR_DP
	ACALL	ACTUALIZAR_PWM
	RET

;*********************************************** ESTADO 5 --> REPOSO TRAS FUNCIONAR

ESTADO5:
        ACALL	GEN_EVE5
        MOV 	A,EVENTO
        RL 	A
        MOV 	DPTR,#EV5_TABL
        JMP 	@A+DPTR


EV5_TABL:
	AJMP	EV50_ESPERA
	AJMP	EV51_COMPROBARTEMP
	AJMP	EV52_MOSTRARHMAYUS
	AJMP	EV53_MOSTRARHMINUS
	AJMP	EV54_MENOR40
	AJMP	EV55_SEGUIR

GEN_EVE5:
	JNB	CALENTANDO, COND52
	MOV 	A, #0X5
	CLR	C
	SUBB	A, MS100
	;SE HA LLEGADO A LOS 500MS
	JZ	COND51
	JB 	TICK100MS,COND55

	;JBC	TICKADC, COND51
	JBC	TICKTEMP, COND52
	RET

EV50_ESPERA:
	RET

COND51:
	MOV 	EVENTO, #01
	RET

COND52:
	MOV 	EVENTO, #04
	RET

COND55:
	MOV	EVENTO, #05
	RET

COMPROBAR_TEMP:
	MOV	MS100, #0
	CLR 	TICK100MS
	ACALL	VER_TEMP
	MOV	A, TEMPERATURA
	CLR 	C
	SUBB	A,#41D
	JC	MENOR80 		;MENOR A 40 GRADOS
	JNC	MAYOR80
	MOV	EVENTO, #00
	RET

EV51_COMPROBARTEMP:
	MOV	MS100,#0 
	ACALL 	ENCENDER_ADC
	ACALL 	COMPROBAR_TEMP
	RET

EV52_MOSTRARHMAYUS:
	MOV 	FUEGO, #12
	ACALL	ACTUALIZAR_DP
	MOV	EVENTO, #00
	RET

EV53_MOSTRARHMINUS:
	MOV	FUEGO, #11 
	ACALL	ACTUALIZAR_DP
	MOV	EVENTO, #00
	RET

EV54_MENOR40:
	CLR	CALENTANDO
	CLR	TICKTEMP
	MOV	ESTADO,#00
	ACALL	APAGAR_DP
	RET

EV55_SEGUIR:
 	CLR 	TICK100MS
	INC 	MS100
	MOV	EVENTO, #00
	RET

MAYOR80:
	MOV 	EVENTO,#02 
	RET

MENOR80:
	MOV	A, TEMPERATURA
	CLR 	C
	SUBB	A,#20D
	JC	MENOR40
	JNC	MAYOR40YMENOR80
	RET

MENOR40:
	SETB	TICKTEMP
        RET

MAYOR40YMENOR80:
	MOV EVENTO, #03
	RET
;------------------------------------------------------------------------------------------------------------------------------------------------



;*********************************************** DISPLAY
ACTUALIZAR_DP:
	MOV	A,FUEGO
	JZ	MOSTRAR_DP_0
	ACALL	BCD_7SEG
	MOV 	DISPLAY, A
	MOV	P0, DISPLAY
	RET

MOSTRAR_DP_0:
	MOV 	FUEGO, #00
	MOV	A, #00
	ACALL 	BCD_7SEG
	MOV 	DISPLAY, A
	MOV 	P0, DISPLAY
	RET

APAGAR_DP:
	CLR 	DP_ENCENDIDO
	MOV	A, #13
	ACALL 	BCD_7SEG
	MOV 	P0, A
	CLR	A
	RET

BCD_7SEG:
	INC 	A
	MOVC 	A,@A+PC
	RET
	DB 	00111111b ;0
	DB 	00000110B ;1
	DB 	01011011B ;2
	DB 	01001111B ;3
	DB 	01100110b ;4
	DB 	01101101B ;5
	DB 	01111101B ;6
	DB 	00000111B ;7
	DB 	01111111B ;8
	DB 	01100111B ;9
	DB	01110011B ;P
	DB	01110100B ;h
	DB 	01110110B ;H
	DB	00000000B ;display apagado

;*********************************************** PWM

ACTUALIZAR_PWM:			
	MOV 	PWMP, #04
	MOV	A, FUEGO
	ACALL 	PWM_TABLA	
	MOV 	PWM0,A
	RET

APAGAR_PWM:			
	MOV 	PWMP, #04
	MOV	A,  #00
	ACALL 	PWM_TABLA	
	MOV 	PWM0,A
	RET

PWM_TABLA:
	INC 	A
	MOVC 	A,@A+PC
	RET
	DB 	11111111B ;0
	DB 	11100110B ;1
	DB 	11001100B ;2
	DB 	10110011B ;3
	DB 	10011001b ;4
	DB 	10000000B ;5
	DB 	01100110B ;6
	DB 	01001101B ;7
	DB 	00110011B ;8
	DB 	00011010B ;9
	DB	00000000B ;P


;*********************************************** ADC

ENCENDER_ADC:
	ANL 	ADCON,#11111000b
	ORL 	ADCON,#00001000b
	ORL 	IEN0,#11000000b
	RET

APAGAR_ADC:
	ANL 	IEN0, #00000000b
	RET

VER_TEMP:
	CLR 	TICKADC
	MOV	TEMPERATURA,ADCH
	CLR 	C
	RET

INT_ADC:
	SETB 	TICKADC
	RET

;*********************************************** TIMER 0	
ENCENDER_TIMER:			
	ORL 	IEN0,#10000010b
	MOV	TH0, #00111000b
	MOV 	TL0, #00111000b
	MOV 	CONT1,#0
	MOV 	CONT2,#0
	SETB 	TR0
	RET

APAGAR_TIMER:			
	ANL 	IEN0, #00000000b
        CLR 	TR0
	RET


INT_TIMER:	
	PUSH 	PSW
	PUSH 	ACC
	INC 	CONT1
	MOV 	A, #01111101b ;125
	CLR 	C
	SUBB 	A, CONT1
	JNZ 	TIMERCON
	MOV 	CONT1, 	#0
	INC 	CONT2
	MOV 	A, #00001000b ;8
	CLR 	C
	SUBB 	A, CONT2
	JNZ 	TIMERCON
	MOV 	CONT2, #0
	SETB 	TICK100MS
	;HAN PASADO 100MS
	POP 	ACC
	POP 	PSW
	RET

TIMERCON:
	POP 	ACC
	POP 	PSW
	RET
;*********************************************** FIN
END
